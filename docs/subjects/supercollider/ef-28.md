# Eli Fieldsteel 28: Writing Classes And Methods

## Intro
[Video link](https://www.youtube.com/watch?v=9gs0WPnxyak)

There is a guide doc called Writing Classes in the docs.

When SC boots it compiles all classes in its core and in two other places:
* User extension directory - only accessible to the logged in user
* System extension directory - accessible to anyone on the machine

To get these two paths run:
```
Platform.userExtensionDir;
Platform.systemExtensionsDir;
```

Save all classes in one of these places (git manage?) and be sure to save as .sc file, not .scd - sc = Supercollider Class.

You can browse classes in the Supercollider IDE using `<class>.browse`.

## Extending existing classes

As an example, here's some boilerplate for adding a class method and an instance method to a class:
```
+ Class {
    newInstanceMethod {
        ...
    }

    *newClassMethod {
        ...
    }
}
```

### Example 1 - tempodur
Where Class can be changed to the a class type - such as Integer, Float, String etc. Note the * to denote a class method. In the next example we are adding an instance method to Integer to calculate how long a beat would last for at that bpm.
```
+ Integer {
    tempodur {
        var beatdur, tempo;
        tempo = this;
        beatdur = 60/tempo;
        ^beatdur;
    }
}
```
Note the use of `this` to refer to the receiver (e.g. `144.tempodur`, where 144 is the receiver) and the use of the caret to explicitly return a value - differing from the standard function practice of returning the last value automatically.

You need to recompile the library in order for the changes to take place.

The issue with the example above is that you would also conceivably call tempodur on a float. Doubling up the code to extend two classes is one solution. But much more OOP is to extend the a parent class shared by each class. The SimpleNumber object contains is extended by both Integer and Float - which both inherit all of the parent's methods.
```
+ SimpleNumber {
    tempodur {
        ...
    }
}
```

### Example 2 - Env.rand
In this example we're going to extend the Env class so it creates a random envelope with a variable amount of segments:
```
+ Env {
    *rand {|n, dur=1|
        var env, levels, times, curves;
        levels = {rrand(-1.0,1.0)}!(n + 1);
        times = {exprand(1,10)}!n;
        times = times.normalizeSum * dur; // scale the times to add up to 1 then * by the required duration
        curves = {rrand(-4.0,4.0)}!n;
        env = this.new(levels,times,curves);
        ^env;
    }
}
```



